apply plugin: 'java-library'

group = 'com.elovirta'
archivesBaseName = 'stylesheetoptimizer'
version = '0.1.0-SNAPSHOT'

description = """DITA Open Toolkit XSLT stylesheet optimizer plug-in."""

repositories {
    mavenCentral()
}
dependencies {
    implementation group: 'org.dita-ot', name: 'dost', version: '3.3.4'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar.archiveFileName = "${project.name}.jar"

task dist(type: Zip, dependsOn: [jar]) {
    from(configurations.archives.artifacts.files) {
        into("lib")
    }
    from(".") {
        include "build.xml"
        include "debug-filter.xml"
        include "xsl/**"
        include "LICENSE"
        include "README.md"
    }
    from(".") {
        include "plugin.xml"
        expand(
                version: project.version
        )
    }
    archiveFileName = "${project.group}.${project.archivesBaseName}-${project.version}.zip"
}
